# Smart Garden

### Duje NikoliÄ‡ Malora

---

# 1. Uvod

## Sustav za automatsko navodnjavanje

Motivacija, a ujedno i cilj odabranog projekta bili su stvoriti **sustav** koji vrÅ¡i **automatsko navodnjavanje** biljaka na temelju oÄitanih vrijednosti iz okoline. Ideja je bila simulirati rad sustava u laboratoriju te pokuÅ¡ati predoÄiti kako bi konceptualno takav sustav funkcionirao ukoliko bi ga se implementiralo meÄ‘u velike plantaÅ¾e i vrtove. Projekt je nazvan ***Smart Garden***.

![Automatic Watering System.jpg](Images/Automatic_Watering_System.jpg)

Da bismo uspjeli realizirati zamiÅ¡ljen proizvod, prvo je bilo potrebno razumijeti kako biljke funkcioniraju, u kojim uvjetima te koje su moguÄ‡e opasnosti za biljku. Odgovore na postavljena pitanja je dala formula **fotosinteze** te ih je objedinila u jedan jedinstven odgovor.

---

## Fotosinteza

> **Fotosinteza** je proces u kojem se sunÄeva energija pretvara u kemijsku pohranjenu u organskim molekulama. Proces gdje iz ugljikova dioksida i vode uz pomoÄ‡ SunÄeve svjetlosti i klorofila nastaje Å¡eÄ‡er i kisik.
> 

![Fotosinteza.jpg](Images/Fotosinteza.jpg)

### **Kemijska formula:**

<aside>
ğŸ’¡ $6CO2Â + 6H2O + light â†’ C6H12O6Â + 6O2$

</aside>

- **6CO2** â€” 6 molekula *ugljikovog dioksida*
- **6H20** â€” 6 molekula vode
- **light** â€” energija SunÄeve svjetlosti
- **C6H12O6** â€” glukoza
- **6O2** â€” 6 molekula kisika

---

## Potrebne informacije za poÄetak rada na projektu

**Voda** je glavni faktor za rast biljaka. Biljka u svakom trenutku mora imati dovoljno vode kako bi mogla vrÅ¡iti proces **fotosinteze**, a na nama je bilo da to osiguramo. Na vlaÅ¾nost tla utjeÄe **temperatura** i **vlaÅ¾nost zraka**, tako da smo za naÅ¡e potrebe trebali mjeriti i ta dva faktora. PomoÄ‡u njihovih trenutnih vrijednosti smo mogli aproksimirati za koliko Ä‡e vremena tlo presuÅ¡iti te na taj naÄin maksimalno optimizirati naÅ¡ sustav.

PomoÄ‡u kemijske formule **fotosinteze** saznali smo takoÄ‘er i da je za rast biljaka uz vodu potrebna i (SunÄeva) svjetlost. S obzirom na to da Sunce ne obasjava istu toÄku na Zemlji cijeli dan potrebno je bilo u sustav dodati joÅ¡ jednu komponentu. Radi se o **LED diodi** koja bi se po potrebi palila i gasila s obzirom na vanjsko osvjetljenje. Ona bi u naÅ¡oj simulaciji predstavljala reflektore koji bi biljkama pruÅ¾ali potrebnu energiju za rast.

---

# 2. Hardware

Kontrolu nad senzorskim osjetilima te radiokomunikaciju vrÅ¡io je **Arduino Uno WiFi Rev2 mikrokontroler** s ugraÄ‘enim WiFi modulom. Glavna komponenta senzorskog Ävora je **elektromotorna pumpa za vodu**.

![Arduino Uno WiFi Rev2.jpg](Images/Arduino_Uno_WiFi_Rev2.jpg)

TakoÄ‘er, kao senzorska osjetila, koriÅ¡teni su **BH1750** â€” senzor razine svjetlosti, **DHT22** â€” senzor temperature i vlaÅ¾nosti zraka te **senzor vlaÅ¾nosti tla**. Za indikaciju nedostatka svjetlosti u okruÅ¾enju, koristili smo **LED diodu**. **Breadboard** je takoÄ‘er posluÅ¾io kako bi sve komponente spojili na Å¾eljene toÄke potencijala (manjak portova na mikrokontroleru). **Relej** je smo koristili kao sklopku koja kontrolira rad elektromotora. Uz sve navedene komponente, potrebne su nam bile i **Å¾ice** kojima smo sve to povezali, te od *vrtnog* hardvera smo koristili: spremnik vode, cijev, izolir traku, posudu za zemlju, te malo vruÄ‡eg ljepila i buÅ¡ilicu kako bi napravili *kuÄ‡iÅ¡te* za elektromotor.

### Lista komponenti i materijala:

- **Arduino Uno WiFi Rev2**
- **elektromotorna pumpa za vodu**
- **BH1750** â€” senzor razine svjetlosti
- **DHT22** â€” senzor temperature i vlaÅ¾nosti zraka
- **senzor vlaÅ¾nosti tla**
- **LED dioda**
- **Breadboard**
- **Relej**
- **Å½ice** â€” M/M, Å½/Å½, M/Å½
- **Spremnik vode**
- **Cijev**
- **Izolir traka**
- **Posuda za zemlju**
- **Ljepilo**
- **BuÅ¡ilica**

![Final product.jpeg](Images/Final_product.jpeg)

---

## Elektromotor â€” mini pumpa za vodu

Glavna komponenta naÅ¡eg sustava je **pumpa za vodu**. Ona omoguÄ‡ava navodnjavanje tako Å¡to elektriÄnu energiju pretvara u mehaniÄku energiju te svojim radom izpumpava vodu iz spremnika u zemlju u koju je posaÄ‘ena biljka.

![Pumpa za vodu.jpg](Images/Pumpa_za_vodu.jpg)

### Karakteristike:

- Napon: **3V-6V**
- Maksimalna visina na koju moÅ¾e podiÄ‡i tekuÄ‡inu: **40-110cm**
- Brzina protoka: **120L/h**
- Vanjski promjer cjevÄice za odvod: **7.5mm**
- Unutarnji promjer cjevÄice za odvod: **4.7mm**
- Dimenzije pumpe: **24mm (promjer), 33mm (visina)**

### Challenge

Jedan od glavnih *hardverskih* problema bio je pronaÄ‡i odgovarajuÄ‡u dimenziju cijevi.

<aside>
ğŸ’¡ $Q = v * A$

</aside>

- ***Q*** â€” volumni protok
- ***v*** â€” brzina fluida u cijevi
- ***A*** â€” presjek cijevi

Iz priloÅ¾ene formule za volumni protok fluida moÅ¾emo zakljuÄiti da su brzina fluida u cijevi i presjek cijevi reciproÄne veliÄine. Za konstantan volumni pritok fluida vrijedi: ako smanjimo presjek cijevi, tada Ä‡emo poveÄ‡ati brzinu fluida u cijevi i obratno. To znaÄi da nam je u interesu bilo da pronaÄ‘emo cijev Å¡to manjeg presjeka, ali dovoljno veliku da je uspijemo spojiti s pumpon za vodu.

Å½elimo postiÄ‡i Å¡to veÄ‡u brzinu vode, jer Å¡to je veÄ‡a brzina vode, to Ä‡e s manje energije dostiÄ‡i veÄ‡u visinu.

### Spajanje

![Spajanje pumpe za vodu.jpg](Images/Spajanje_pumpe_za_vodu.jpg)

### Challenge

Iz sheme spajanja vidimo da elektromotor spajamo na **napajanje (5V/3.3V)** ili **uzemljenje (GND)**. Ovdje dolazimo do novog izazova. Kada spojimo direktno elektromotor na napajanje i uzemljenje, strujni krug je zatvoren za cijelo vrijeme i elektromotor neprestano radi. Mi Å¾elimo postiÄ‡i **kontroliran** rad elektromotora. Problem smo pokuÅ¡ali rijeÅ¡iti spajanjem na **digitalne pinove**, no to nije pomoglo. Digitalni pinovi imaju istu razinu napona (5V) kao i pin za direktno napajanje, meÄ‘utim, **struja** na digitalnim pinovima je znatno **manja**. Na kraju, pronaÅ¡li smo idealno, tek malo kompliciranije rjeÅ¡enje â€” **relej**.

### Relej

Relej je posluÅ¾io kao sklopka. Relej se sastoji od 3 pina s donje strane:

- **VCC** â€” napajanje
- **GND** â€” uzemljenje
- **IN** â€” prima impulse elektriÄnog signala

**IN** pin smo spojili sa digitalnim pinom preko kojeg Ä‡emo slati impulse releju, a relej Ä‡e na temelju tih impulsa otvarati i zatvarati strujni krug.

![Relej.jpg](Images/Relej.jpg)

S gornje strane ima 3 slota. Na jedan slot smo spojili uzemljenje Arduina, a na drugi uzemljenje elektromotora. Å½icu na napajanje od elektromotora smo spojili na izvor napajanja s Arduina.

### Spajanje â€” relej kao sklopka

![Spajanje sa relejom.png](Images/Spajanje_sa_relejom.png)

### Vrtni hardware

Kao Å¡to je veÄ‡ prethodno spomenuto, potreban nam je bio jedan spremnik vode unutar kojeg Ä‡e biti uronjena pumpa za vodu. Na stijenkama plastiÄnog spremnika napravili smo rupu tek dovoljno Å¡iroku da mogu proÄ‡i Å¾ice za napajanje elektromotora. Na krovu spremnika napravili smo prolaz za cijev koja bi tu cijev dodatno fiksirala i usmjerila prema posudi za zemlju. Izolir trakom smo uÄvrstili pumpu za vodu kako nebi *plesala* dok je uronjena u vodu. Drugi dio cijevi je vodio u posudu za zemlju gdje se nalazila zemlja koja treba biti navodnjavana.

![Spremnik za vodu (otvoren).jpeg](Images/Spremnik_za_vodu_(otvoren).jpeg)

![Spremnik za vodu (zatvoren).jpeg](Images/Spremnik_za_vodu_(zatvoren).jpeg)

![Cijev za vodu.jpeg](Images/Cijev_za_vodu.jpeg)

![Posuda za zemlju.jpeg](Images/Posuda_za_zemlju.jpeg)

![Spremnik za vodu + cijev.jpeg](Images/Spremnik_za_vodu_+_cijev.jpeg)

![Posuda za zemlju + cijev.jpeg](Images/Posuda_za_zemlju_+_cijev.jpeg)

![Vrtni hardware.jpeg](Images/Vrtni_hardware.jpeg)

---

## BH1750 â€” Senzor razine osvjetljenja

Da bismo znali kada paliti, a kada gasiti **LED diodu**, bio nam je potreban senzor koji Ä‡e pratiti razinu osvjetljenja u naÅ¡em u okruÅ¾enju. Za tu potrebu smo koristili **BH1750 senzor**.

### **Pinout**:

- **VCC** â€” napajanje (3.3V ili 5V)
- **GND** â€” ****uzemljenje
- **SCL** â€” â€œSerial clockâ€ za I2C komunikaciju
- **SDA** â€” â€œSerial dataâ€ za I2C komunikaciju
- **ADDR** â€” selektor adrese

### Spajanje

![BH1750 spajanje.jpeg](Images/BH1750_spajanje.jpeg)

![BH1750 laboratorij.jpeg](Images/BH1750_laboratorij.jpeg)

### Challenge

U poÄetnoj fazi razvoja projekta, koristili smo **Arduino Uno** mikrokontroler. Gore, sa *sheme* moÅ¾emo vidjeti da se **SDA** i **SDL** pinovi senzora spajaju na analogne inpute **A4** i **A5**. Ovakvo spajanje je moguÄ‡e jer pinovi A4 i A5 predstavljaju alternativu za pinove SDA i SCL kao Å¡to je vidljivo iz slike ispod.

![Uno Pinout.png](Images/Uno_Pinout.png)

TakoÄ‘er, moguÄ‡e je spajanje direktno na SDA i SCL pinove locirane kraj **AREF** pina. MeÄ‘utim, situacija je drukÄija kod **Arduino Uno WiFi Rev2** mikrokontrolera. Kod Rev2 mikrokontrolera, pinovi A4 i A5 **ne predstavljaju** alternativu za SDA i SCL pinove, veÄ‡ su iskljuÄivo analog input pinovi. Ovime je Rev2 na obiÄni UNO izgubio alternativu za I2C pinove, ali dobio je dva dodatna analog input pina.

![Pinout Rev2.jpeg](Images/Pinout_Rev2.jpeg)

Za zakljuÄiti je da bismo onda kao idealno rjeÅ¡enje kod Rev2 mikrokontrolera pinove BH1750 senzora spojili direktno na SDA i SCL pinove, meÄ‘utim, u laboratoriju to nije funkcioniralo. MoguÄ‡e da je problem bio u industrijskoj greÅ¡ci samog mikrokontrolera.

![Uno Lux 2.png](Images/Uno_Lux_2.png)

![Rev2 Lux.png](Images/Rev2_Lux.png)

Kako je cijeli projekt zamiÅ¡ljen kao simulacija velikog sustava za automatsko navodnjavanje, onda je optimalno rjeÅ¡enje bilo simulirati rad BH1750 senzora pomoÄ‡u ***random*** funkcije. Ukoliko bi *random* funkcija vraÄ‡ala vrijednost koja je ispod nekog ***thresholda*** LED dioda bi se upalila, inaÄe bi ostala ugaÅ¡ena.

### LED dioda

**LED diodu** smo koristili u kombinaciji sa **BH1750** senzorom. Na temelju oÄitanja senzora se LED dioda palila, odnosno gasila. Kao ***threshold*** smo koristili vrijednost od 100 luxa.

<aside>
ğŸ’¡ **lux** â€” koliÄina osvjetljenja koja se dobiva kada je jedan lumen ravnomjerno rasporeÄ‘en na povrÅ¡ini od jednog kvadratnog metra

</aside>

S obzirom da je LED-ica **dioda** morali smo paziti da pravilno spojimo **anodu** i **katodu**. Anoda se spaja na **pozitivni** pol napajanja, dok se katoda spaja na **negativni**. Distinktivno ih moÅ¾emo razlikovati jer je noÅ¾ica od anode neÅ¡to **duÅ¾a** od noÅ¾ice katode.

![LED dioda.png](Images/LED_dioda.png)

---

## DHT22 â€” Senzor temperature i vlaÅ¾nosti zraka

Za naÅ¡ projekt, odluÄili smo da dio vaÅ¾nosti damo **temperaturi** i **vlaÅ¾nosti zraka**, kako njihov utjecaj na vlaÅ¾nost samog tla **nije zanemariv**. Ovaj senzor igrao je kljuÄnu ulogu u **uÅ¡tedi energije** jer smo na temelju njegovih oÄitanja stvorili **proraÄun** za koliko Ä‡e se tek **vremena** senzor vlaÅ¾nosti tla upaliti, odnosno **kada** â€” ***timer***.

### **Pinout**:

- **VCC** â€” napajanje (3.3V ili 5V)
- **DATA** â€” ****Å¡alje podatke o temperaturi i o vlaÅ¾nosti zraka
- **NC** â€” *no connection*, ne koristi se
- **GND** â€” uzemljenje

### Spajanje

![BH1750 i DHT22 spajanje.png](Images/BH1750_i_DHT22_spajanje.png)

![DHT22 laboratorij.jpeg](Images/DHT22_laboratorij.jpeg)

### Challenge

Kao Å¡to smo veÄ‡ spomenuli, **DHT22** senzor igrat Ä‡e veliku ulogu kod odluÄivanja kada oÄitati vlaÅ¾nost tla. Trebalo je smisliti formulu koja bi na temelju ulaznih vrijednosti (temperatura i vlaÅ¾nost tla) davala na izlazu broj sekundi za koliko Ä‡e se upaliti senzor tla. **Temperatura** linearno raste. VaÅ¾no je za napomenuti kako 20Â°C nije dvostruko toplije od 10Â°C, dok 300K uistinu jeste dvostruko toplije od 150K. Temperaturi smo dali vaÅ¾nost od **90%** u konaÄnoj formuli. LogiÄno je da porastom temperature se treba smanjiti vrijeme izmeÄ‘u dva oÄitanja vlaÅ¾nosti tla jer visoke temperature brÅ¾e isuÅ¡uju tlo. Stoga smo temperaturu promatrali **reciproÄno** u konaÄnoj formuli. S druge strane, **vlaÅ¾nost zraka** se izraÅ¾ava u postotcima, od 0 do 100 posto. LogiÄno je zakljuÄiti da veÄ‡a vlaÅ¾nost zraka moÅ¾e znaÄiti i veÄ‡u vlaÅ¾nost tla, meÄ‘utim, ova informacija znaÄi puno manje od temperature, pa smo vlaÅ¾nosti zraka dali vaÅ¾nost od **10%**.

<aside>
ğŸ’¡ $**âŒŠ100/temp * 0.9 + hum * 0.1âŒ‹ * 10**$

</aside>

| Temperatura zraka | VlaÅ¾nost zraka | IzraÄunato vrijeme |
| --- | --- | --- |
| 20Â°C | 45% | 90s |
| 30Â°C | 60% | 90s |
| 35Â°C | 50% | 70s |

---

## Senzor vlaÅ¾nosti tla

**Senzor vlaÅ¾nosti tla** koristimo kako bismo odredili kada Ä‡emo dovoditi vodu u zemlju.

Senzor je dovoljno paliti svako **12 sati**, ali mi nastojimo simulirati taj rad, tako da smo, kao Å¡to je veÄ‡ spomenuto, na vlastitu odgovornost izraÄunali period paljenja senzora (uz pomoÄ‡ **DHT22 senzora**).

![Soil-Moisture-Sensor-Pinout.png](Images/Soil-Moisture-Sensor-Pinout.png)

### Pinout:

- **VCC** â€” napajanje (3.3V ili 5V)
- **GND** â€” uzemljenje
- **DO** â€” ****digital output
- **AO** â€” analog output

### Spajanje

Ukoliko bismo spojili senzor na digitalni output, na *serial monitoru* bi dobivali vrijednosti 0 i 1 (bool), zato ga spajamo na analogni output kako bi na *serial monitoru* dobili dekadske vrijednosti i na temelju njih definirali razinu vlaÅ¾nosti tla.

![Spajanje.png](Images/Spajanje.png)

![Senzor vlaznosti tla laboratorij.jpeg](Images/Senzor_vlaznosti_tla_laboratorij.jpeg)

### Princip rada senzora

Princip rada senzora vlaÅ¾nosti tla je vrlo jednostavan za shvatiti. Senzor se u zemlji ponaÅ¡a kao **potenciometar**, odnosno **mjeri otpor** unutar medija. Å to je tlo vlaÅ¾nije, odnosno Å¡to ima viÅ¡e vode, to Ä‡e medij bit **konduktivniji** pa Ä‡e otpor biti manji. S druge strane, Å¡to je tlo viÅ¡e suho, odnosno Å¡to ima manje vode u tlu, to Ä‡e medij bit **otporniji** pa Ä‡e i otpor biti veÄ‡i.

![Kako radi senzor vlaÅ¾nosti tla.png](Images/Kako_radi_senzor_vlanosti_tla.png)

Stoga na temelju oÄitanja s analognog outputa definiramo **dvije granice**, odnosno **tri stanja** vlaÅ¾nosti tla. 

- **manje od 500** â€” tlo je mokro
- **izmeÄ‘u 500 i 750** â€” optimalna razina vlaÅ¾nosti
- **veÄ‡e od 750** â€” tlo je suho

> **NAPOMENA:** Senzor kojeg smo mi koristili za potrebe naÅ¡eg projekta sklon je **koroziji**. Tako da se preporuÄa da se povremeno zamijeni novim. Ovo je ujedno i najveÄ‡a mana koriÅ¡tenog senzora.
> 

---

# 3. Software

## **Funkcionalnost osvjetljenja**

### BH1750_init()

```cpp
BH1750 lightMeter(0x23);

void SENSORS::BH1750_init(){
  lightMeter.begin(BH1750::CONTINUOUS_HIGH_RES_MODE);
  Serial.println(F("BH1750 test!"));
}
```

<aside>
#ï¸âƒ£ Funkciju koristimo kako bi **inicijalizirali** rad senzora BH1750.

</aside>

### readLight()

```cpp
BH1750 lightMeter(0x23);

uint16_t SENSORS::readLight() {
    uint16_t lux = lightMeter.readLightLevel();

    Serial.print("Light: ");
    Serial.print(lux);
    Serial.println(" lx");

    return lux;
}
```

<aside>
#ï¸âƒ£ Deklariramo varijablu *lux*. Varijabla je tipa ***uint16_t*** Å¡to znaÄi da moÅ¾e poprimiti maksimalnu vrijednost **65536** (odnosno 2^16). 8 bitova bi bilo premalo jer razina svjetlosti ponekad ide i preko 256 lx, pa smo se oprjedjelili za 16. Unutar funkcije se poziva metoda readLightLevel() koja vraÄ‡a vrijednost oÄitanu sa senzora. VraÄ‡enu vrijednost pridjeljujemo varijabli ***lux***. Unutar funkcije takoÄ‘er na ***serial monitor*** ispisujemo razinu trenutnog osvjetljenja u okruÅ¾enju. Funkcija vraÄ‡a **vrijednost varijable *lux***.

</aside>

### calculateRandomLight()

```cpp
#include <stdlib.h>

uint16_t calculateRandomLight() {
    srand(millis());

    int randomLight = rand() % 50 + 90;

    Serial.print("Light: ");
    Serial.print(randomLight);
    Serial.println(" lx");

    return randomLight;
}
```

<aside>
#ï¸âƒ£ S obzirom na prethodno navedeni problem gdje BH1750 senzor ne radi na Rev2 mikrokontroleru, bilo je potrebno smisliti funkciju koja Ä‡e pruÅ¾iti alternativu kalkuliranja trenutne razine osvjetljenja u okruÅ¾enju. S obzirom da Å¾elimo simulirati rad sustava, dovoljno je bilo napisati funkciju koja Ä‡e **nasumiÄno** vraÄ‡ati â€œrazinu osvjetljenja u okruÅ¾enju senzoraâ€. KoriÅ¡tene su funkcije ***srand()*** i ***rand()***. Funkcija *rand()* vraÄ‡a nasumiÄnu vrijednost u intervalu **od 90 do 140**. S obzirom da je naÅ¡ ***threshold* 100** vjerojatnost je 4:1, odnosno **80%** da se Å¾arulja upali (ostane upaljena), a **20%** da se Å¾arulja ugasi (ostane ugaÅ¡ena).

</aside>

### blinkLed()

```cpp
#define led 13

typedef enum {
  READ_SERIAL,
  READ_SENSORS,
  TURNON_LED,
  TURNOFF_LED
} StateType;

StateType ledState = READ_SERIAL;

uint16_t lux = 0;

void setup() {
  Serial.begin(9600);

	pinMode(led, OUTPUT);	

	lux = calculateRandomLight();

  delay(100);
}

void loop() {
	lux = calculateRandomLight();
	blinkLed(lux);

	delay(2000);
}

void blinkLed(uint16_t lux) {
  switch (ledState) {
    case READ_SERIAL:
      ledState = READ_SENSORS;
      break;
    
    case READ_SENSORS:
      if (lux < 100)
        ledState = TURNON_LED;
      else
        ledState = TURNOFF_LED;
      break;

    case TURNON_LED:
      digitalWrite(led, HIGH);
      ledState = READ_SERIAL;
      break;

    case TURNOFF_LED:
      digitalWrite(led, LOW);
      ledState = READ_SERIAL;
      break;

    default:
      break;
  }
	
	return;
}
```

<aside>
#ï¸âƒ£ Paljenje i gaÅ¡enje LED diode smo realizirali preko **state machine**. Unutar state machine definirali smo 4 stanja: READ_SERIAL, READ_SENSORS, TURNON_LED, TURNOFF_LED. Inicijalno smo postavili ***ledState*** u READ_SERIAL. TakoÄ‘er, morali smo definirati da nam se LED dioda nalazi na **digitalnom pinu 13** te pozivom funkcije ***pinMode()*** postaviti taj pin u OUTPUT naÄin rada. Unutar ***loop()*** funkcije se poziva funkcija ***calculateRandomLight()*** te se njena vraÄ‡ena vrijednost prosljeÄ‘uje funkciji ***blinkLed()*** koja tu vrijednost prima kao parametar. Unutar funkcije ***blinkLed()*** poziva se **state machine**. Ukoliko parametar funkcije, ***lux*** prelazi ***threshold*** **100**, tada se LED dioda funkcijom ***digitalWrite()*** gasi (odnosno, ostaje ugaÅ¡ena), a ukoliko ne prelazi, tada se LED dioda istom funkcijom pali (odnosno, ostaje upaljena). Nakon poziva funkcije ***digitalWrite()*** *ledState* se nanovo stavlja u stanje **READ_SERIAL** te cijeli proces provjere kreÄ‡e ispoÄetka. VaÅ¾no je za primijetiti da Ä‡e, ukoliko je to potrebno, do **promjene** doÄ‡i tek izmeÄ‘u **drugog** i **Äetvrtog poziva *loop()*** funkcije. Razlog je oÄit, ukoliko je vrijednost ***lux*** varijable izazvala promjenu, do promjene na LED diodi Ä‡e doÄ‡i tek nakon Å¡to se proÄ‘e kroz sva navedena stanja.

</aside>

---

## Mjerenje temperature i vlaÅ¾nosti zraka te proraÄun *timera*

### DHT_init()

```cpp
#define DHTPIN 2
#define DHTTYPE DHT22

DHT dht(DHTPIN, DHTTYPE);

void SENSORS::DHT_init(){
  Serial.println(F("DHTxx test!"));
  dht.begin();
}
```

<aside>
#ï¸âƒ£ Funkciju koristimo kako bi **inicijalizirali** rad senzora DHT22.

</aside>

### readTemp()

```cpp
float temperature;

float SENSORS::readTemp() {
    temperature = dht.readTemperature();

    if(isnan(temperature)) {
        Serial.println("Failed to read temperature from DHT sensor!");
        return -1;
    }

    Serial.print("Temperature: ");
    Serial.print(temperature);
    Serial.println(" Â°C");

    return temperature;
}
```

<aside>
#ï¸âƒ£ Deklariramo varijablu ***temperature***. Varijabla je tipa ***float*** jer varijabla moÅ¾e poprimiti i decimalne vrijednosti. Unutar funkcije se dohvaÄ‡a vrijednost sa **DHT22** senzora. Ukoliko je ona uspjeÅ¡no dohvaÄ‡ena, ispisuje se trenutna temperature te funkcija vraÄ‡a njenu **vrijednost**. Ukoliko nije uspjeÅ¡no dohvaÄ‡ena, ispisuje se *error message* te funkcija vraÄ‡a **-1**.

</aside>

### readHum()

```cpp
float humidity;

float SENSORS::readHum() {
    humidity = dht.readHumidity();

    if(isnan(humidity)) {
        Serial.println("Failed to read humidity from DHT sensor!");
        return -1;
    }

    Serial.print("Humidity: ");
    Serial.print(humidity);
    Serial.println("%");

    return humidity;
}
```

<aside>
#ï¸âƒ£ Deklariramo varijablu ***humidity***. Varijabla je tipa ***float*** jer varijabla moÅ¾e poprimiti i decimalne vrijednosti. Unutar funkcije se dohvaÄ‡a vrijednost sa **DHT22** senzora. Ukoliko je ona uspjeÅ¡no dohvaÄ‡ena, ispisuje se trenutna vlaÅ¾nost zraka te funkcija vraÄ‡a njenu **vrijednost**. Ukoliko nije uspjeÅ¡no dohvaÄ‡ena, ispisuje se *error message* te funkcija vraÄ‡a **-1**.

</aside>

### calculateCurrentTime()

```cpp
int calculateCurrentTime() {
    return (int)(millis() / 1000);
}
```

<aside>
#ï¸âƒ£ Funkcija ***millis()*** vraÄ‡a vrijeme u milisekundama koje je proteklo od poÄetka rada mikrokontrolera. Ukoliko to podijelimo s 1000 i zaokruÅ¾imo, dobijemo vrijeme u sekundama, Å¡to upravo funkcija ***calculateCurrentTime()*** vraÄ‡a.

</aside>

### calculateTime()

```cpp
int calculateTime(float temp, float hum) {
    int calculatedTime = floor((100/temp) * 0.9 + hum * 0.1) * 10;    
    
    return calculatedTime;
}
```

<aside>
#ï¸âƒ£ Funkcija **raÄuna** **za koliko** Ä‡e se **vremena upaliti senzor** â€” vraÄ‡a cjelobrojnu vrijednost koja predstavlja broj sekundi. RaÄuna se na temelju **formule** koja je ranije prokomentirana.

</aside>

### checkTimer()

```cpp
int checkTimer() {
  temp = sensor.readTemp();
  hum = sensor.readHum();

  int startTime = calculateCurrentTime();
  int calculatedTime = calculateTime(temp, hum);

  int sum = startTime + calculatedTime;

  Serial.print("Timer: ");
  Serial.println(sum);

  return sum;
}
```

<aside>
#ï¸âƒ£ Unutar funkcije se pozivaju funkcije ***readTemp()*** i ***readHum()***. VraÄ‡ene vrijednosti se prosljeÄ‘uju funkciji ***calculateTime()*** koja vraÄ‡a za koliko Ä‡e se vremena upaliti senzor vlaÅ¾nosti tla. Funkcijom ***calculateCurrentTime()*** dohvaÄ‡amo koliko je vremena proÅ¡lo od poÄetka rada mikrokontrolera. Zbrajanjem **trenutnog vremena** i **vremena za koliko Ä‡e se upaliti senzor ****dobivamo vrijednost koja predstavlja vremenski trenutak **kada Ä‡e se upaliti senzor â€” *timer***.

</aside>

---

## Funkcionalnost automatskog navodnjavanja

### readSoil()

```cpp
#define sensorPower 8
#define sensorPin A0

#define soilWet 500
#define soilDry 750

int timer = 0;

void setup() {
	Serial.begin(9600);
	
	pinMode(sensorPower, OUTPUT);

	digitalWrite(sensorPower, LOW);

	timer = checkTimer();

	delay(100);
}

void loop() {
	int currentTime = calculateCurrentTime();

  Serial.print("Current time: ");
  Serial.println(currentTime);

	if (timer <= currentTime) {
    soil = readSoil();
    pumpWater(soil);
  }

	delay(2000);
}

int readSoil() {
  digitalWrite(sensorPower, HIGH);
  delay(10);

  int soil = analogRead(sensorPin);
  digitalWrite(sensorPower, LOW);

  Serial.print("Soil: ");
  Serial.println(soil);

  return soil;
}
```

<aside>
#ï¸âƒ£ Definiramo dvije graniÄne vrijednosti: ***soilWet*** i ***soilDry***. ***soilWet*** predstavlja maksimalnu vrijednost za koju tlo smatramo vlaÅ¾nim, dok ***soilDry*** predstavlja minimalnu vrijednost za koju tlo smatramo suhim. TakoÄ‘er definiramo ***sensorPower*** pin, pomoÄ‡u kojeg Ä‡emo kontrolirat paljenje i gaÅ¡enje senzora i inicijalno ga postavljamo u ***LOW***. Ovime osiguravamo da senzor nije stalno napajan i time Å¡tedimo energiju. TakoÄ‘er definiramo ***sensorPin*** koji predstavlja analogni pin s kojeg Ä‡emo dohvaÄ‡at podatke. Unutar ***loop()*** funkcije se poziva funkcija ***readSoil()*** ukoliko je trenutno vrijeme veÄ‡e ili jednako od   ***timer-a***. Funkcija ***readSoil()*** popriliÄno je jednostavna za shvatiti. Na poÄetku funkcije pomoÄ‡u funkcije ***digitalWrite()*** napajamo senzor. KoriÅ¡tenjem funkcije ***analogRead()*** dohvaÄ‡amo podatke s analognog inputa. Nakon Å¡to smo proÄitali vrijednost sa senzora, ponovnim pozivom funkcije ***digitalWrite()*** prestajemo napajati senzor, a vrijednost i ispisujemo i vraÄ‡amo nazad.

</aside>

### pumpWater()

```cpp
#define motor 7

typedef enum {
  READ_SERIAL,
  READ_SENSORS,
  PUMP_WATER
} StateType;

StateType soilState = READ_SERIAL;

int timer = 0;

void setup() {
	Serial.begin(9600);
	
	pinMode(motor, OUTPUT);

	timer = checkTimer();

	delay(100);
}

void loop() {
	int currentTime = calculateCurrentTime();

  Serial.print("Current time: ");
  Serial.println(currentTime);

	if (timer <= currentTime) {
    soil = readSoil();
    pumpWater(soil);
  }

	delay(2000);
}

void pumpWater(int soil) {
  switch (soilState) {
    case READ_SERIAL:
      soilState = READ_SENSORS;
      break;
    
    case READ_SENSORS:
      if (soil >= soilDry)
        soilState = PUMP_WATER;
      else if (soil <= soilWet) {
        soilState = READ_SERIAL;
        timer = checkTimer();
      }
      else
        soilState = READ_SERIAL;
      break;

    case PUMP_WATER:
      digitalWrite(motor, HIGH);
      delay(1000);
      digitalWrite(motor, LOW);
      timer = checkTimer();
			soilState = READ_SERIAL;
      break;

    default:
      break;
  }

  return;
}
```

<aside>
#ï¸âƒ£ Rad elektromotora, odnosno pumpanje vode smo realizirali preko **state machine**. Unutar state machine definirali smo 3 stanja: READ_SERIAL, READ_SENSORS, PUMP_WATER. Inicijalno smo postavili ***soilState*** u READ_SERIAL. TakoÄ‘er, morali smo definirati da nam se elektromotor nalazi na **digitalnom pinu 7** te pozivom funkcije ***pinMode()*** postaviti taj pin u OUTPUT naÄin rada. Unutar ***loop()*** funkcije se poziva funkcija ***pumpWater()*** ukoliko je trenutno vrijeme veÄ‡e ili jednako od ***timer-a***. Unutar funkcije ***pumpWater()*** poziva se **state machine**. Postoje **tri sluÄaja** koja se mogu dogoditi: tlo je **suho**, tlo je **vlaÅ¾no** i tlo je **otpimalne vlaÅ¾nosti**. Ukoliko je tlo suho, tada se jednu sekundu tlo navodnjava te se zavrÅ¡etkom navodnjavanja raÄuna novi ***timer*** i stavlja se u **READ_SERIAL** stanje.  Ukoliko je tlo vlaÅ¾no, raÄuna se novi ***timer*** i stavlja se u **READ_SERIAL** stanje. No, ukoliko je tlo optimalne vlaÅ¾nosti, tada se svakim pozivom funkcije prebacuje izmeÄ‘u stanja **READ_SERIAL** **i **READ_SENSORS**.

</aside>

---

# 4. Wireless communication

Å to se samog sustava za navodnjavanje tiÄe, beÅ¾iÄna komunikacija **nije potrebna**. MeÄ‘utim, za potrebe kolegija je bilo potrebno implementirati neku vrstu **beÅ¾iÄne komunikacije**.

BeÅ¾iÄnu komunikaciju nam je omoguÄ‡io mikrokontroler â€” **Arduino Uno WiFi Rev2**.

RjeÅ¡enje je bilo stvoriti varijaciju beÅ¾iÄnog **praÄ‡enja trenutnih oÄitanja** na senzorskom Ävoru. To smo postigli s ***access pointom***. PomoÄ‡u mikrokontrolera smo stvorili ***access point*** preko kojeg smo uparili ureÄ‘aj koji ima moguÄ‡nost povezivanja preko **WiFi-a**. U naÅ¡em sluÄaju to je bio laptop na kojem se cijeli projekt i radio.

Unutar koda koji je odgovoran za beÅ¾iÄnu komunikaciju nalazi se i dio **HTML** koda koji na **web clientu** ispisuje informacije. U kodu takoÄ‘er definiramo **SSID** i **password** pomoÄ‡u kojih Ä‡e se ureÄ‘aj spojit na ***access point***. Kod takoÄ‘er vrÅ¡i provjeru je li ureÄ‘aj spojen uspjeÅ¡no te je li instalirana ispravna verzija ***firmware-a***. Web client se ***refresha*** svakih 5 sekundi kako bi ostali aÅ¾urni s informacijama na senzorskom Ävoru.

![Access point 2.png](Images/Access_point_2.png)

![Wireless connection 2.png](Images/Wireless_connection_2.png)

![Connected to access point 2.png](Images/Connected_to_access_point_2.png)

![Terminal - access point.png](Images/Terminal_-_access_point.png)

### enableWiFi()

```cpp
void enableWiFi() {
  if (WiFi.status() == WL_NO_MODULE) {
    Serial.println("Communication with WiFi module failed!");
    while (true);
  }

  String fv = WiFi.firmwareVersion();

  if (fv < WIFI_FIRMWARE_LATEST_VERSION) {
    Serial.println("Please upgrade the firmware");
  }
}
```

<aside>
#ï¸âƒ£ Funkciju koristimo za provjeru je li komunikacija s **WiFi modulom** uspjeÅ¡na i za provjeru ispravnosti verzije ***firmware-a***.

</aside>

### connectWiFi()

```cpp
void connectWiFi() {
  Serial.print("Creating access point named: ");
  Serial.println(ssid);

  status = WiFi.beginAP(ssid, pass);
  if (status != WL_AP_LISTENING) {
    Serial.println("Creating access point failed");
    while (true);
  }
}
```

<aside>
#ï¸âƒ£ Funkcija sluÅ¾i za stvaranje i spajanje ureÄ‘aja na ***access point***.

</aside>

### printWiFiStatus()

```cpp
void printWiFiStatus() {
  Serial.print("SSID: ");
  Serial.println(WiFi.SSID());

  IPAddress ip = WiFi.localIP();
  Serial.print("IP Address: ");
  Serial.println(ip);

  Serial.print("To see this page in action, open a browser to http://");
  Serial.println(ip);
}
```

<aside>
#ï¸âƒ£ Funkcija sluÅ¾i da bi na *serial monitor* ispisali **SSID** te da bi ispisali i **IP adresu** web clienta na kojeg Ä‡emo se spojit kako bi pratili informacija sa senzorskog Ävora.

</aside>

### printWeb()

```cpp
void printWeb() {
  if (client) {                             // if you get a client,
    // Serial.println("new client");           // print a message out the serial port
    String currentLine = "";                // make a String to hold incoming data from the client
    while (client.connected()) {            // loop while the client's connected
      if (client.available()) {             // if there's bytes to read from the client,
        char c = client.read();             // read a byte, then
        // Serial.write(c);                    // print it out the serial monitor
        if (c == '\n') {                    // if the byte is a newline character

          // if the current line is blank, you got two newline characters in a row.
          // that's the end of the client HTTP request, so send a response:
          if (currentLine.length() == 0) {

            // HTTP headers always start with a response code (e.g. HTTP/1.1 200 OK)
            // and a content-type so the client knows what's coming, then a blank line:
            client.println("HTTP/1.1 200 OK");
            client.println("Content-type:text/html");
            client.println("Refresh: 5");
            client.println();
           
            //create the buttons
            client.print("<p>Temperature: ");
            client.print(temp);
            client.print(" degree Celsius</p>");

            client.print("<p>Humidity: ");
            client.print(hum);
            client.print("%</p>");

            client.print("<p>Light: ");
            client.print(lux);
            client.print(" lx</p>");

            client.print("<p>Soil: ");
            client.print(soil);
            client.print("</p>");
            // The HTTP response ends with another blank line:
            client.println();
            // break out of the while loop:
            break;
          }
          else {      // if you got a newline, then clear currentLine:
            currentLine = "";
          }
        }
        else if (c != '\r') {    // if you got anything else but a carriage return character,
          currentLine += c;      // add it to the end of the currentLine
        }
      }
    }
    // close the connection:
    client.stop();
    // Serial.println("client disconnected");
  }
}
```

<aside>
#ï¸âƒ£ Funkcija sluÅ¾i kako bismo **dohvatili** informacije te kako bismo **upisali** informacije na web client.

</aside>

---

# 5. Source code

## main.cpp

```cpp
#include "Sensors.h"
#include "Calculation.h"
#include "Wireless.h"

#define motor 7
#define sensorPower 8
#define sensorPin A0
#define led 13

#define soilWet 500
#define soilDry 750

SENSORS sensor;

typedef enum {
  READ_SERIAL,
  READ_SENSORS,
  TURNON_LED,
  TURNOFF_LED,
  PUMP_WATER
} StateType;

StateType ledState = READ_SERIAL;
StateType soilState = READ_SERIAL;

int timer = 0;
float temp = 0;
float hum = 0;
uint16_t lux = 0;
int soil = 0;

int checkTimer();
void blinkLed(uint16_t lux);
void pumpWater(int soil);
int readSoil();

char ssid[] = "smartgarden";
char pass[] = "smartgarden123";
int keyIndex = 0;
int status = WL_IDLE_STATUS;
WiFiServer server(80);

WiFiClient client = server.available();

void connectWiFi();
void printWeb();

void setup() {
  Serial.begin(9600);

  Serial.println("Access Point Web Server");
  enableWiFi();
  connectWiFi();

  delay(10000);

  server.begin();

  printWiFiStatus();

  Serial.println("");
  
  pinMode(motor, OUTPUT);
  pinMode(sensorPower, OUTPUT);
  pinMode(led, OUTPUT);

  digitalWrite(sensorPower, LOW);

  sensor.DHT_init();

  Serial.println("");

  timer = checkTimer();
  lux = calculateRandomLight();

  Serial.println("");

  delay(100);
}

void loop() {
  lux = calculateRandomLight();

  int currentTime = calculateCurrentTime();

  Serial.print("Current time: ");
  Serial.println(currentTime);

  blinkLed(lux);

  if (timer <= currentTime) {
    soil = readSoil();
    pumpWater(soil);
  }

  if (status != WiFi.status()) {
    status = WiFi.status();

    if (status == WL_AP_CONNECTED) {
      Serial.println("Device connected to AP");
    } else {
      Serial.println("Device disconnected from AP");
    }
  }

  client = server.available();

  if (client) {
    printWeb();
  }

  Serial.println("");

  delay(2000);
}

int checkTimer() {
  temp = sensor.readTemp();
  hum = sensor.readHum();

  int startTime = calculateCurrentTime();
  int calculatedTime = calculateTime(temp, hum);

  int sum = startTime + calculatedTime;

  Serial.print("Timer: ");
  Serial.println(sum);

  return sum;
}

void blinkLed(uint16_t lux) {
  switch (ledState) {
    case READ_SERIAL:
      ledState = READ_SENSORS;
      break;
    
    case READ_SENSORS:
      if (lux < 100)
        ledState = TURNON_LED;
      else
        ledState = TURNOFF_LED;
      break;

    case TURNON_LED:
      digitalWrite(led, HIGH);
      ledState = READ_SERIAL;
      break;

    case TURNOFF_LED:
      digitalWrite(led, LOW);
      ledState = READ_SERIAL;
      break;

    default:
      break;
  }

  return;
}

void pumpWater(int soil) {
  switch (soilState) {
    case READ_SERIAL:
      soilState = READ_SENSORS;
      break;
    
    case READ_SENSORS:
      if (soil >= soilDry)
        soilState = PUMP_WATER;
      else if (soil <= soilWet) {
        soilState = READ_SERIAL;
        timer = checkTimer();
      }
      else
        soilState = READ_SERIAL;
      break;

    case PUMP_WATER:
      digitalWrite(motor, HIGH);
      delay(1000);
      digitalWrite(motor, LOW);
      timer = checkTimer();
      soilState = READ_SERIAL;
      break;

    default:
      break;
  }

  return;
}

int readSoil() {
  digitalWrite(sensorPower, HIGH);
  delay(10);

  int soil = analogRead(sensorPin);
  digitalWrite(sensorPower, LOW);

  Serial.print("Soil: ");
  Serial.println(soil);

  return soil;
}

void connectWiFi() {
  Serial.print("Creating access point named: ");
  Serial.println(ssid);

  status = WiFi.beginAP(ssid, pass);
  if (status != WL_AP_LISTENING) {
    Serial.println("Creating access point failed");
    while (true);
  }
}

void printWeb() {
  if (client) {                             // if you get a client,
    // Serial.println("new client");           // print a message out the serial port
    String currentLine = "";                // make a String to hold incoming data from the client
    while (client.connected()) {            // loop while the client's connected
      if (client.available()) {             // if there's bytes to read from the client,
        char c = client.read();             // read a byte, then
        // Serial.write(c);                    // print it out the serial monitor
        if (c == '\n') {                    // if the byte is a newline character

          // if the current line is blank, you got two newline characters in a row.
          // that's the end of the client HTTP request, so send a response:
          if (currentLine.length() == 0) {

            // HTTP headers always start with a response code (e.g. HTTP/1.1 200 OK)
            // and a content-type so the client knows what's coming, then a blank line:
            client.println("HTTP/1.1 200 OK");
            client.println("Content-type:text/html");
            client.println("Refresh: 5");
            client.println();
           
            //create the buttons
            client.print("<p>Temperature: ");
            client.print(temp);
            client.print(" degree Celsius</p>");

            client.print("<p>Humidity: ");
            client.print(hum);
            client.print("%</p>");

            client.print("<p>Light: ");
            client.print(lux);
            client.print(" lx</p>");

            client.print("<p>Soil: ");
            client.print(soil);
            client.print("</p>");
            // The HTTP response ends with another blank line:
            client.println();
            // break out of the while loop:
            break;
          }
          else {      // if you got a newline, then clear currentLine:
            currentLine = "";
          }
        }
        else if (c != '\r') {    // if you got anything else but a carriage return character,
          currentLine += c;      // add it to the end of the currentLine
        }
      }
    }
    // close the connection:
    client.stop();
    // Serial.println("client disconnected");
  }
}
```

## Sensors.h

```cpp
#ifndef Sensors_h
#define Sensors_h

#include <Arduino.h>
#include <Adafruit_Sensor.h>
#include <DHT.h>
#include <Wire.h>
#include <BH1750.h>

#define DHTPIN 2
#define DHTTYPE DHT22

class SENSORS {
public:
        SENSORS();
        ~SENSORS();
        void DHT_init();
        void BH1750_init();
        float readTemp();
        float readHum();
        uint16_t readLight();
};

#endif
```

## Sensors.cpp

```cpp
#include "Sensors.h"

DHT dht(DHTPIN, DHTTYPE);
BH1750 lightMeter(0x23);

SENSORS::SENSORS(){}
SENSORS::~SENSORS(){}

float humidity, temperature;

void SENSORS::DHT_init(){
  Serial.println(F("DHTxx test!"));
  dht.begin();
}

void SENSORS::BH1750_init(){
  lightMeter.begin(BH1750::CONTINUOUS_HIGH_RES_MODE);
  Serial.println(F("BH1750 test!"));
}

float SENSORS::readTemp() {
    temperature = dht.readTemperature();

    if(isnan(temperature)) {
        Serial.println("Failed to read temperature from DHT sensor!");
        return -1;
    }

    Serial.print("Temperature: ");
    Serial.print(temperature);
    Serial.println(" Â°C");

    return temperature;
}

float SENSORS::readHum() {
    humidity = dht.readHumidity();

    if(isnan(humidity)) {
        Serial.println("Failed to read humidity from DHT sensor!");
        return -1;
    }

    Serial.print("Humidity: ");
    Serial.print(humidity);
    Serial.println("%");

    return humidity;
}

uint16_t SENSORS::readLight() {
    uint16_t lux = lightMeter.readLightLevel();

    Serial.print("Light: ");
    Serial.print(lux);
    Serial.println(" lx");

    return lux;
}
```

## Calculation.h

```cpp
#ifndef Calculation_h
#define Calculation_h

#include <math.h>
#include <time.h>
#include <stdlib.h>

int calculateTime(float temp, float hum) {
    int calculatedTime = floor((100/temp) * 0.9 + hum * 0.1) * 10;    
    
    return calculatedTime;
}

int calculateCurrentTime() {
    return (int)(millis() / 1000);
}

uint16_t calculateRandomLight() {
    srand(millis());

    int randomLight = rand() % 50 + 90;

    Serial.print("Light: ");
    Serial.print(randomLight);
    Serial.println(" lx");

    return randomLight;
}

#endif
```

## Wireless.h

```cpp
#ifndef Wireless_h
#define Wireless_h

#include <WiFiNINA.h>
#include <WiFi.h>

void enableWiFi() {
  if (WiFi.status() == WL_NO_MODULE) {
    Serial.println("Communication with WiFi module failed!");
    while (true);
  }

  String fv = WiFi.firmwareVersion();

  if (fv < WIFI_FIRMWARE_LATEST_VERSION) {
    Serial.println("Please upgrade the firmware");
  }
}

void printWiFiStatus() {
  Serial.print("SSID: ");
  Serial.println(WiFi.SSID());

  IPAddress ip = WiFi.localIP();
  Serial.print("IP Address: ");
  Serial.println(ip);

  Serial.print("To see this page in action, open a browser to http://");
  Serial.println(ip);
}

#endif
```

---

# 6. Final product

![Final product 2.jpeg](Images/Final_product_2.jpeg)

![Full example 2.png](Images/Full_example_2.png)

---

# 7. Literatura

- [https://hr.wikipedia.org/wiki/Fotosinteza](https://hr.wikipedia.org/wiki/Fotosinteza)
- [https://hr.wikipedia.org/wiki/Volumni_protok](https://hr.wikipedia.org/wiki/Volumni_protok)
- [https://e-radionica.com/hr/blog/2018/12/10/kkm-mini-pumpa-za-vodu/](https://e-radionica.com/hr/blog/2018/12/10/kkm-mini-pumpa-za-vodu/)
- [https://github.com/toperkov/WiSe-2021-22/blob/main/instructions/lab-2.md](https://github.com/toperkov/WiSe-2021-22/blob/main/instructions/lab-2.md)
- [https://lastminuteengineers.com/soil-moisture-sensor-arduino-tutorial/](https://lastminuteengineers.com/soil-moisture-sensor-arduino-tutorial/)
- [https://docs.arduino.cc/tutorials/uno-wifi-rev2/uno-wifi-r2-hosting-a-webserver](https://docs.arduino.cc/tutorials/uno-wifi-rev2/uno-wifi-r2-hosting-a-webserver)
- [https://docs.arduino.cc/tutorials/uno-wifi-rev2/uno-wifi-r2-web-server-ap-mode](https://docs.arduino.cc/tutorials/uno-wifi-rev2/uno-wifi-r2-web-server-ap-mode)